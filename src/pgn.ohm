PGN {

Game = Tags Moves

Tags = TagPair*

TagPair = "[" tagName tagValue "]"
tagName = symbol
tagValue = string

Moves = ElementSequence gameState?

ElementSequence = ( element | RecursiveVariation )*

RecursiveVariation = "(" ElementSequence ")"

element
  = endOfLineComment
  | inlineComment
  | moveNumber
  | san annotation? --move

endOfLineComment
  = ";" endOfLineCommentChar* newline
endOfLineCommentChar
  = ~newline any

moveNumber = digit+ moveDots
moveDots = "." | "..."

inlineComment
  = "{" inlineCommentChar* "}"
inlineCommentChar
  = ~"}" any

san = symbol

annotation = ( "?" | "!" ) ( "?" | "!" )?

gameState
  = "1-0"     -- white
  | "0-1"     -- black
  | "1/2-1/2" -- draw
  | "*"       -- ongoing

newline = "\r"? "\n"

string = "\"" stringChar* "\""
stringChar
  = "\\\""
  | ~"\\" ~"\"" ~"\n" any

symbol = symbolStart symbolPart*
symbolStart = alnum
symbolPart = alnum | "_" | "+" | "#" | "=" | ":" | "-"

}
